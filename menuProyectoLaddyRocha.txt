// Funciones o SubAlgoritmo  del proyecto
// SubAlgoritmo menus del proyecto
Funcion opc=presentarMenu(titulo,menu,lim)
	Definir opc Como Caracter
	Definir pos Como Entero
	Borrar Pantalla
	Escribir titulo
	Para pos=0 Hasta lim-1 Con Paso 1 Hacer
		Escribir menu[pos]
	Fin Para
	Escribir "       Elija opción[1..",lim,"]" Sin Saltar
	leer opc
FinFuncion
//Ejercicios Estructura selectiva
//Ejercicio 1.
//¿Siguiendo la prioridad de operadores, convierta a expresión matemática,
//resuelva e indique en cuál tipo de variable almacenará el resultado de las
//siguientes expresiones:
Funcion expresionMatematica()
	Definir resultado1 como logico
	Definir resultado2 como entero
	Definir resultado3 como entero
	Definir resultado4 como logico
	Definir resultado5 como logico
	
	resultado1 <- (5 + 3 * 2) + 9 > 3 * 5 * 14 % 3
	resultado2 <- 2 * (4 - 10 + 8) / 2 * 36 * (1 / 2)
	resultado3 <- 260 / 12 + 54 % 3 - 85 % 7
	resultado4 <- (48 < 2 * 3) | (2 * 7 < 12)
	resultado5 <- ((8 > 2) | (932 < 23)) & 4 == 2
	
	Escribir "El resultado de la expresión 1 es:", resultado1
	Escribir "El resultado de la expresión 2 es:", resultado2
	Escribir "El resultado de la expresión 3 es:", resultado3
	Escribir "El resultado de la expresión 4 es:", resultado4
	Escribir "El resultado de la expresión 5 es:", resultado5
FinFuncion

//Ejercicio 2.
//Dados dos (2) números calcule la suma, resta, multiplicación, división y módulo.
Funcion operaciones ()
	Definir num1,num2 Como Entero
	Escribir "Ingrese numero 1"
	leer num1
	Escribir "Ingrese numero 2"
	leer num2
	suma=num1+num2
	Escribir "la suma de " num1 "+" num2 " es = " suma 
	resta=num1-num2
	Escribir "la resta de " num1 "-" num2 " es = " resta 
	multiplicacion=num1*num2
	Escribir "la multiplicacion de " num1 "*" num2 " es = " multiplicacion
	division=num1/num2
	Escribir "la division de " num1 "/" num2 "es = " division
	modulo=num1 mod num2
	Escribir  "el modulo de la division de " num1 "y " num2 " es " modulo
FinFuncion
//Ejercicio 3
//Dados tres (3) números, Hacer una aplicación que calcule la resolvente.
Funcion resolvente()
	Definir a, b, c, discr como reales
	Escribir "Ingrese el valor de a:"
	Leer a
	Escribir "Ingrese el valor de b:"
	Leer b
	Escribir "Ingrese el valor de c:"
	Leer c
	discr <- b^2 - 4*a*c
	Si discr > 0 Entonces
		x1 <- (-b + raiz(discr)) / (2*a)
		x2 <- (-b - raiz(discr)) / (2*a)
		Escribir "La ecuación tiene dos soluciones reales:"
		Escribir "x1 =", x1
		Escribir "x2 =", x2
	Sino 
		Si discr = 0 Entonces
			x1 = (-b / (2*a))
			Escribir "La ecuación tiene una solución real única:"
			Escribir "x =", x1
		Sino
			Escribir "La ecuación no tiene soluciones reales."
		FinSi
	FinSi	
	Escribir "La resolvente es:"
	Escribir "(-b ± raiz (b^2 - 4ac)) / (2a)"
	Escribir "(-", b, " ± raiz(", b, "^2 - 4 *", a, "*", c, ")) / (2 *", a, ")"
	Escribir "(-", b, " ± raiz(", b^2 - 4*a*c, ")) /", 2*a
FinFuncion
//Ejercicio 4
//Dados dos lados de un triángulo en cm, calcular la hipotenusa del mismo.
funcion hipotenusa()
	Definir lado1, lado2, hipo como real
	Escribir "Ingrese el lado 1 del triangulo en centimentros"
	Leer lado1
	EScribir "Ingrese el lado 2 del triangulo en centimetros"
	Leer lado2
	hipo=raiz (lado1^2 + lado2^2)
	Escribir "La hipotenusa del triangulo es: " hipo
FinFuncion
//Ejercicio 5
//Dado un número, imprimir 0 si es par y 1 si es impar.
Funcion par0Impar1()
	Definir num como entero
	Escribir "Ingrese un  numero"
	leer num
	Si num mod 2 = 0  entonces
		Escribir "0" 
	SiNo
		EScribir "1"
	FinSi
FinFuncion
//Ejercicio 6
//Dado un (1) número binario de cuatro (4) dígitos imprimir su bit da paridad. El bitde
//paridad es 1 si el número de bits 1 es impar y 0 en caso contrario.
Funcion CalcularParidad()
	Definir numBinario como cadena
	Escribir "Ingrese un número binario de cuatro dígitos:"
	Leer numBinario
	Definir cont como entero
	cont = 0
	Para i <- 1 Hasta 4 Con Paso 1 Hacer
		Si Subcadena(numBinario, i, i) = "1" Entonces
			cont = cont + 1
		FinSi
	FinPara
	Definir bitParidad como entero
	Si cont % 2 = 0 Entonces
		bitParidad <- 0
	Sino
		bitParidad <- 1
	FinSi
	Escribir "El bit de paridad es:", bitParidad
FinFuncion
//Ejercicio 6.2
//Dado un (1) número binario de cuatro (4) dígitos imprimir su valor
Funcion ImprimirValorBinario()
    Definir numeroBinario como cadena
    Definir valorDecimal como entero
    Escribir "Ingrese un número binario de cuatro dígitos:"
    Leer numeroBinario
    valorDecimal <- 0
    Para i <- 0 Hasta 3 Hacer
        carac <- Subcadena(numeroBinario, i, i)
        valo =  carac
        valorDecimal <- valorDecimal * 2 + valor
    FinPara
    Escribir "El valor decimal es:", valorDecimal
FinFuncion
//Ejercicio 7
//Dado un (1) número de cuatro (4) dígitos imprimirlo por separado en unidades,decenas,
//centenas y unidades de mil.
Funcion digitosSeparados()
    Definir num, unidades, decenas, centenas, unidadDeMil como entero
    Escribir "Ingrese un número de cuatro dígitos:"
    Leer num
    Si num >= 1000 y num <= 9999 Entonces
        unidades <- num mod 10
        num <- trunc(num / 10)
        decenas <- num mod 10
        num <- trunc(num / 10)
        centenas <- num mod 10
        num <- trunc(num / 10)
        unidadDeMil <- num

        Escribir "Unidades de mil:", unidadDeMil
        Escribir "Centenas:", centenas
        Escribir "Decenas:", decenas
        Escribir "Unidades:", unidades
    Sino
        Escribir "Error: El número ingresado no tiene cuatro cifras."
    FinSi
FinFuncion
//Ejercicio 8
//Dado un carácter indicar si es un digito o una consonante o un carácter especial.
Funcion IdentificarCaracter()
    Definir car como caracter 
	Escribir " Ingrese caracter "
	Leer car
	Si car>="0" y car<="9" Entonces
		Escribir car " es un digito "
	SiNo
		Si car<>"a" o car<>"e" o car<>"i" o car<>"o" o car<>"u" o car<>"A" o car<>"E" o car<>"I" o car<>"O" o car<>"U"  Entonces
  			Escribir "El carácter" car " es una consonante."
		Sino
			Escribir "El carácter " car " es un carácter especial."
		FinSi
	FinSi
FinFuncion
//Ejercicio 9
//Dado dos caracteres indicar si el primer carácter es igual, mayor o menor que el segundo
Funcion caracterIgualMayormenor()
	Definir car1, car2 como caracter
	Escribir "Ingrese primer caracter:"
	Leer car1
	Escribir "Ingrese segundo caracter:"
	Leer car2
	car1=Mayusculas(car1)
	car2=Mayusculas(car2)
	Si car1 = car2 Entonces
		Escribir "El primer carácter es igual al segundo carácter."
	Sino 
		SI car1<car2 Entonces
			Escribir "El primer carácter es menor que el segundo carácter."
		Sino
			si car1>car2 Entonces
				Escribir "El primer carácter es mayor que el segundo carácter."
			FinSi
		FinSi
	FinSi
FinFuncion
//Ejercicio 10
//Dada una frase cualquiera presentarla en mayúscula y minuscula
Funcion fraseMayusculaMinuscula()
	Definir frase Como Caracter
	Escribir "Ingrese una frase"
	Leer frase
	Escribir Mayusculas(frase)
	Escribir Minusculas(frase)
FinFuncion
//Ejercicios con estructuras condicionales 
//Ejercicio 1.
//Todos los años que se dividen exactamente entre 400, o que son divisibles exactamente
//entre 4 y no son divisibles exactamente entre 100 son años bisiestos.Usando estas
//premisas crea un algoritmo que lea una fecha como un en tres variables: aaaa,mes, dia, y
//luego indique si el año de la fecha es un año bisiesto o no.
funcion AñoBisiesto()
    Definir anio, mes, dia como enteros
	Escribir "Ingrese el año:"
    Leer año
    Escribir "Ingrese el mes:"
    Leer mes
    Escribir "Ingrese el día:"
    Leer dia
    Si (año mod 400 = 0) O ((año mod 4 = 0) Y (año mod 100 <> 0)) Entonces
        Escribir "El año ", año, " es un año bisiesto."
    Sino
        Escribir "El año ", año, " no es un año bisiesto."
    FinSi
Finfuncion
//Ejercicio 2.
//Dado un número entero cuya cantidad de dígitos es igual a 5, determine si escapicúa.
//Nota: un número capicúa es aquel que se lee igual hacia adelante que hacia atrás.
// Algoritmo principal del proyecto
funcion EsCapicua()
	Definir num, a, b como entero
	Escribir "Ingrese un número entero de 5 dígitos:"
	Leer num
	a = trunc (num/10000)
	b=num mod 10
	Si a==b Entonces
		Escribir "El número ", num, " es capicúa."
	SiNo
		Escribir "El número ", num, " no es capicúa"
	FinSi
Finfuncion
//Ejercicio 3
//Cree un algoritmo que tome por entrada las horas y minutos de un día y dé comoresultado
//su equivalente en segundos.
Funcion equivalenteSegundos()
	Definir hors, mins, segs como entero
    Escribir "Ingrese las horas:"
    Leer hors
    Escribir "Ingrese los minutos:"
    Leer mins
	segs <- (hors * 3600) + (mins * 60)
	Escribir "El equivalente en segundos es: ", segs
FinFuncion
//Ejercicio 3.2
//Para un valor entero positivo que representa una cantidad en segundos, indicarsu
//equivalente en minutos, horas y días
Funcion segundosAdiasHorasMinutos()
	Definir segs, mins, hors, dias como entero
    Escribir "Ingrese la cantidad de segundos:"
    Leer segs
    mins <- trunc(segs / 60)
    segs <- segs mod 60
    hors <- trunc(mins / 60)
    mins <- mins mod 60
    dias <- trunc(hors / 24)
    hors <- hors mod 24
    Escribir "Equivalente en días:", dias
    Escribir "Equivalente en horas:", hors
    Escribir "Equivalente en minutos:", mins
    Escribir "Equivalente en segundos:", segs
FinFuncion
//Ejercicio 4
//Dados tres números enteros positivos A, B y C, ¿Determine si son iguales? ¿cuál de ellos es
//elmayor? y ¿cuál es el segundo mayor?
Funcion tresIgualMayorSegundoMayor()
	Definir A, B, C como entero
    Escribir "Ingrese el valor de A:"
    Leer A
    Escribir "Ingrese el valor de B:"
    Leer B
    Escribir "Ingrese el valor de C:"
    Leer C
    Si A = B y B = C Entonces
        Escribir "Los tres números son iguales."
    Sino
        Si A > B y A > C Entonces
            Escribir "El número mayor es A." A
        Sino
            Si B > A y B > C Entonces
                Escribir "El número mayor es B." B
            Sino
                Escribir "El número mayor es C." C
            FinSi
        FinSi
    FinSi
    
    Si A <> B y A <> C y B <> C Entonces
        Si A > B y A > C Entonces
            Si B > C Entonces
                Escribir "El segundo mayor es B." B
            Sino
                Escribir "El segundo mayor es C." C
            FinSi
        Sino
            Si B > A y B > C Entonces
                Si A > C Entonces
                    Escribir "El segundo mayor es A." A
                Sino
                    Escribir "El segundo mayor es C." C
                FinSi
            Sino
                Si A > B Entonces
                    Escribir "El segundo mayor es A." A
                Sino
                    Escribir "El segundo mayor es B." B
                FinSi
            FinSi
        FinSi
    Sino
        Escribir "No hay un segundo mayor, al menos dos números son iguales."
    FinSi
FinFuncion
//Ejercicio 5.
//En un estacionamiento el monto a pagar se calcula multiplicando el número dehoras que
//permaneció el automóvil dentro del estacionamiento por $1.5 la hora.La fracion en
//minutos de (1- 29 minutos) se cobra $0.5. Pasado de 30 minutos se cobra el valor de la
//hora
//Ahora se desea que usted elabore un algoritmo que a partir de la hora de entraday la
//	hora de salida de un vehículo (las mismas corresponden a un mismo día) calcule el monto
//	a pagar por el dueño del vehículo.
//	La entrada vendrá dada por dos enteros positivos el primero representa las horasy el
//	segundo los minutos
Funcion montoEstacionamiento()
    Definir horaEntrada, minEntrada, horaSalida, minSalida como entero
    Definir totalHors, totalMins, montoPagar como real
    Escribir "Ingrese la hora de entrada del vehículo (en formato de 24 horas):"
    Leer horaEntrada
    Escribir "Ingrese los minutos de entrada del vehículo:"
    Leer minEntrada
    Escribir "Ingrese la hora de salida del vehículo (en formato de 24 horas):"
    Leer horaSalida
    Escribir "Ingrese los minutos de salida del vehículo:"
    Leer minSalida
    totalHors = horaSalida - horaEntrada
    totalMins = minSalida - minEntrada
    Si totalMins < 0 Entonces
        totalHors = totalHors - 1
        totalMins = 60 + totalMins
    FinSi
    Si totalMins <= 29 Entonces
        montoPagar = totalHors * 1.5 + 0.5
    Sino
        montoPagar = (totalHors + 1) * 1.5
    FinSi
    Escribir "El monto a pagar por el estacionamiento es de: $", montoPagar
FinFuncion
//Ejercicio 6
//El IMC resulta de la división de la masa del individuo (en kilogramos) entre el cuadrado de
//la estatura (en metros). El índice de masa corporal es un indicadordel peso de una
//persona en relación con su altura.
//Clasificación del IMC de acuerdo con la OMS de la ONU:
//a. Menor a 16: Criterio de ingreso.
//b. 16 a 16.9: infra peso.
//c.17 a 18.4: bajo peso.
//d. 18.5 a 24.9: peso normal.
//e. 25 a 29.9: sobrepeso.
//f. 30 a 34.9: obesidad pre-mórbida.
//g. 40 a 45: obesidad mórbida.
//h. Mayor a 45: obesidad híper-mórbida.
//	Dado el peso de una persona en libras (1 libra = 0,453592 Kg) y su estatura en
//	centímetros, calcule su IMC e indique como salida el peso en kilogramos, el valorde IMC
//	de la persona y la categoría en la cual fue clasificado.
//Ejercicio 7.
//Escriba un algoritmo que reciba una fecha (día y mes) correspondiente al año2014 e
//imprima por pantalla el número de días que han pasado desde el 1 deEnero de 2014
//hasta la fecha dada.
funcion año2014()
	Definir dia, mes, diasPasados como entero
    Escribir "Ingrese el día:"
    Leer dia
    Escribir "Ingrese el mes:"
    Leer mes
    diasPasados = dia - 1  // Restamos 1 para no contar el día actual
    Si mes >= 2 Entonces
        diasPasados = diasPasados + 31  // Enero
    FinSi
    Si mes >= 3 Entonces
        diasPasados = diasPasados + 28  // Febrero
    FinSi
    Si mes >= 4 Entonces
        diasPasados = diasPasados + 31  // Marzo
    FinSi
    Si mes >= 5 Entonces
        diasPasados = diasPasados + 30  // Abril
    FinSi
    Si mes >= 6 Entonces
        diasPasados = diasPasados + 31  // Mayo
    FinSi
    Si mes >= 7 Entonces
        diasPasados = diasPasados + 30  // Junio
    FinSi
    Si mes >= 8 Entonces
        diasPasados = diasPasados + 31  // Julio
    FinSi
    Si mes >= 9 Entonces
        diasPasados = diasPasados + 31  // Agosto
    FinSi
    Si mes >= 10 Entonces
        diasPasados = diasPasados + 30  // Septiembre
    FinSi
    Si mes >= 11 Entonces
        diasPasados = diasPasados + 31  // Octubre
    FinSi
    Si mes >= 12 Entonces
        diasPasados = diasPasados + 30  // Noviembre
    FinSi
    Escribir "Número de días pasados desde el 1 de Enero de 2014:", diasPasados
FinFuncion
//Ejercicio 8
//Solicitar un número entre el 1 y el 12 e imprimir el mes correspondiente a dicho número
Funcion numeroDemeses()
Definir numeroMes como entero
Escribir "Ingrese un número entre 1 y 12:"
Leer numMes
Segun numMes Hacer
	"1":
		Escribir "Enero"
	"2":
		Escribir "Febrero"
	"3":
		Escribir "Marzo"
	"4":
		Escribir "Abril"
	"5":
		Escribir "Mayo"
	"6":
		Escribir "Junio"
	"7":
		Escribir "Julio"
	"8":
		Escribir "Agosto"
	"9":
		Escribir "Septiembre"
	"10":
		Escribir "Octubre"
	"11":
		Escribir "Noviembre"
	"12":
		Escribir "Diciembre"
	De Otro Modo:
		Escribir "El número ingresado no es válido."
FinSegun
FinFuncion
//Ejercicio 9.
//En un almacén se hace un 20% de descuento a los clientes cuya compra supere los $1000,
//se desea que realice un algoritmo el cual tome por entrada el monto apagar por el cliente
//y arroje como salida el monto aplicando el descuento de ser necesario.
Funcion descuento20compra1000()
	Definir montoAPagar, montoConDsct como real
    Escribir "Ingrese el monto a pagar por el cliente:"
    Leer montoAPagar
    Si montoAPagar > 1000 Entonces
        montoConDsct = montoAPagar - (montoAPagar * 0.2)
        Escribir "Monto a pagar con descuento:", montoConDsct
    Sino
        Escribir "Monto a pagar:", montoAPagar
    FinSi
FinFuncion
//Ejercicio 10.
//Dado dos números y un operador matemático(+,-,*,/,mod,div) realizar la suma, resta,
//multiplicación, división, resto y división entera(div) de los dos números según el operador
//ingresado.
Funcion dosNumerosYoperador()
	Definir num1, num2, result como real
    Definir ope como cadena
    Escribir "Ingrese el primer número:"
    Leer num1
    Escribir "Ingrese el segundo número:"
    Leer num2
    Escribir "Ingrese el operador matemático (+, -, *, /, mod, trunc):"
    Leer ope
    Segun ope Hacer
		'+':
            result = num1 + num2
            Escribir "Suma:", result
		'-':
            result = num1 - num2
            Escribir "Resta:", result
		'*':
            result = num1 * num2
            Escribir "Multiplicación:", result
		'/':
            result = num1 / num2
            Escribir "División:", result
		'mod':
            result = num1 mod num2
            Escribir "Resto:", result
	    'div':
			result= trunc(num1 / num2)
            Escribir "División entera:", result
		De Otro Modo:
			Escribir "Operador no válido."
    FinSegun
FinFuncion
//3) Estructuras Iterativas
//Ejercicio 1.
//Dado un número entero N, calcular e informar al usuario cuántos dígitos tiene dicho
//número.
Funcion digitosN()
	Definir N, cont como entero
    Escribir "Ingrese un número entero:"
    Leer N
    cont = 0
    Mientras N <> 0 Hacer
        N=trunc(N /10)
        cont = cont+ 1
    Fin Mientras
    Escribir "El número tiene ", cont, " dígitos."
FinFuncion
//Ejercicio 2.
//Dado un número, determine si es capicúa.
//Nota: un número capicúa es aquel que se lee igual hacia adelante que hacia atrás.
funcion NumeroCapicua()
    Definir numero, numeroRev, dig, temp como Entero
    Escribir "Ingrese un número:"
    Leer num
    numeroRev = 0
    temp = num
    Mientras temp > 0 Hacer
        dig = temp mod 10
        numeroRev = numeroRev * 10 + dig
        temp = trunc(temp / 10)
    Fin Mientras
    Si num = numeroRev Entonces
        Escribir "El número es capicúa."
    Sino
        Escribir "El número no es capicúa."
    FinSi
FinFuncion
//Ejercicio 3.
//Escribir un algoritmo que presente los divisores de un numero
Funcion divisores()
	Definir numero, div como Entero
    Escribir "Ingrese un número:"
    Leer num
    Escribir "Los divisores de ", num, " son:"
    Para div = 1 Hasta num Hacer
        Si num mod div = 0 Entonces
            Escribir div
        FinSi
    FinPara
FinFuncion
//Ejercicio 4
//Escribir un algoritmo que presente la suma de los divisores de un numero
Funcion sumaDivisores()
	Definir num, divisor, suma como Entero
    Escribir "Ingrese un número:"
    Leer num
    suma = 0
    Para div = 1 Hasta num Hacer
        Si num % div = 0 Entonces
            suma = suma + div
        FinSi
    FinPara
    Escribir "La suma de los divisores de ", num, " es: ", suma
FinFuncion
//Ejercicio 5
//Escribir un algoritmo que presente la cantidad de los divisores de un numero
Funcion cantidadDivisores()
	Definir num, div, cant como Entero
    Escribir "Ingrese un número:"
    Leer num
    cant = 0
    Para div = 1 Hasta num Hacer
        Si num mod div = 0 Entonces
            cant = cant + 1
        FinSi
    FinPara
    Escribir "La cantidad de divisores de ", num, " es: ", cant
FinFuncion
//Ejercicio 6.
//Escribir un algoritmo que indique si un número es perfecto
//Nota: un número es perfecto cuando la suma de los divisores del número incluido el 1 y
//excluido el propio número es igual al numero
//Ejemplo: numero=6: los divisores del 6 son: 1+2+3=6
Funcion numPerfecto()
Definir num, sumaDiv como Entero
Escribir "Ingrese un número:"
Leer num
sumaDiv = 0
Para div = 1 Hasta num-1 Hacer
	Si num mod div = 0 Entonces
		sumaDiv = sumaDiv + div
	FinSi
FinPara
Si sumaDiv = num Entonces
	Escribir "El número ", num, " es perfecto."
Sino
	Escribir "El número ", num, " no es perfecto."
FinSi
FinFuncion
//Ejercicio 7
//Dado un número N determinar si es un número primo.
//Nota: Un número primo es aquel que solo es divisible por 1(uno) y por el mismo.
Funcion Nprimo()
	Definir num, esPrimo como Entero
    esPrimo = 1
    Escribir "Ingrese un número:"
    Leer num
    Si num < 2 Entonces
        esPrimo = 0
    Sino
        Para div = 2 Hasta trunc(num / 2) Hacer
            Si num mod div = 0 Entonces
                esPrimo = 0
            FinSi
        FinPara
    FinSi
    Si esPrimo = 1 Entonces
        Escribir "El número", num, "es primo."
    Sino
        Escribir "El número", num, "no es primo."
    FinSi
FinFuncion
//Ejercicio 8.
//Construya un programa que dado un valor entero N, haga el cálculo de la función factorial
//utilizando estructuras iterativas.
Funcion Nfactorial()
	Definir N como Entero
	dEFINIR fact Como Real
    fact = 1
    Escribir "Ingrese un número:"
    Leer N
    Si N >= 0 Entonces
        Para i = 1 Hasta N Hacer
            fact = fact * i
        FinPara
        Escribir "El factorial de ", N, " es: ", fact
    Sino
        Escribir "Error: El número debe ser mayor o igual a cero."
    FinSi
FinFuncion
//Ejercicio 9.
//Dado un número entero N que representa una contraseña y asumiendo que una
//contraseña debe tener al menos 10 dígitos para ser segura, determine si la contraseña
//ingresada por el usuario es correcta, de no serlo debe pedirla nuevamente hasta que tenga
//los 10 (diez) dígitos solicitados y al ser correcta mostrar un mensaje de éxito al usuario, por
//salida estándar.
Funcion contraseña()
	Definir contrasena como Cadena
    Mientras Longitud(contrasena) < 10 Hacer
        Escribir "Ingrese una contraseña (debe tener al menos 10 dígitos):"
        Leer contrasena
        Si Longitud(contrasena) < 10 Entonces
            Escribir "La contraseña ingresada no cumple con los requisitos. Intente nuevamente."
        FinSi
    FinMientras
    Escribir "Contraseña ingresada correctamente. ¡Éxito!"
FinFuncion
//Ejercicio 10.
//Dada una secuencia de números terminada en cero (0), elaborar un algoritmo que informe
//al usuario qué valor tiene el número mayor y qué valor tiene el número menor, sin contar
//el cero (0).
Funcion valorMayorValorMenor()
	Definir num, valorMayor, valorMenor como Entero
    Escribir "Ingrese una secuencia de números (ingrese 0 para terminar):"
    Leer num
    Si num = 0 Entonces
        Escribir "No se ingresaron números."
    Sino
        valorMayor = num
        valorMenor = num
        Repetir
            Si num <> 0 Entonces
                Si num > valorMayor Entonces
                    valorMayor = num
                FinSi
                Si num < valorMenor Entonces
                    valorMenor = numero
                FinSi
            FinSi
            Leer num
        Hasta que num = 0
        Escribir "El valor mayor es:", valorMayor
        Escribir "El valor menor es:", valorMenor
    FinSi
FinFuncion
//Ejercicio 11.
//Se tiene una secuencia de enteros terminada en cero, que corresponde a la edad, peso y
//estatura de una muestra de hombres y mujeres mayores de 18 años. Con base en dicha
//secuencia se desea realizar un estudio a fin de conocer:
//Edad promedio de todas las personas en la muestra.
//Peso promedio de todas las personas en la muestra.
//Estatura promedio de todas las personas en la muestra.
//Cuántas personas hay con edad entre los 18 y 25 años.
//Cuántas personas son mayores a 36 años.
//Cuál es el promedio de peso de las personas con edades entre 18 y 35 años.
Funcion  edad18y35()
	Definir edad, peso, estatura, contador, sumaEdad, sumaPeso, sumaEstatura, contador18_25, contadorMayor36, contador18_35, sumaPeso18_35 como Entero
    Definir promEdad, promPeso, promEstatura, promPeso18_35 como Real
	
    contador <- 0
    sumaEdad <- 0
    sumaPeso <- 0
    sumaEstatura <- 0
    contador18_25 <- 0
    contadorMayor36 <- 0
    contador18_35 <- 0
    sumaPeso18_35 <- 0
	
    Escribir "Ingrese la edad, el peso y la estatura de las personas (ingrese 0 en la edad para terminar):"
    Escribir "edad"
	Leer edad
	Escribir "peso"
    Leer peso 
	Escribir "estatura"
    Leer estatura
	
    Mientras edad <> 0 Hacer
        contador <- contador + 1
        sumaEdad <- sumaEdad + edad
        sumaPeso <- sumaPeso + peso
        sumaEstatura <- sumaEstatura + estatura
        Si edad >= 18 y edad <= 25 Entonces
            contador18_25 <- contador18_25 + 1
        FinSi
        Si edad > 36 Entonces
            contadorMayor36 <- contadorMayor36 + 1
        FinSi
        Si edad >= 18 y edad <= 35 Entonces
            contador18_35 <- contador18_35 + 1
            sumaPeso18_35 <- sumaPeso18_35 + peso
        FinSi
        Leer edad, peso, estatura
    FinMientras
    promEdad <- sumaEdad / contador
    promPeso <- sumaPeso / contador
    promEstatura <- sumaEstatura / contador
    promPeso18_35 <- sumaPeso18_35 / contador18_35
    Escribir "Edad promedio de la muestra:", promEdad
    Escribir "Peso promedio de la muestra:", promPeso
    Escribir "Estatura promedio de la muestra:", promEstatura
    Escribir "Cantidad de personas con edad entre 18 y 25 años:", contador18_25
    Escribir "Cantidad de personas mayores a 36 años:", contadorMayor36
    Escribir "Promedio de peso de las personas con edades entre 18 y 35 años:", promPeso18_35
FinFuncion
//Ejercicio 12.
//Construye un algoritmo que calcule e imprima la tabla de multiplicar, desde la tabla del 1
//hasta la del 10.
Funcion tablas1al10()
	Definir i, j como Entero
    Para i = 1 Hasta 10 Hacer
        Escribir "Tabla de multiplicar del ", i, ":"
        Para j = 1 Hasta 10 Hacer
            Escribir i, " x ", j, " = ", i * j
        FinPara
        Escribir "-----------------------"
    FinPara
FinFuncion
//Ejercicio 13.
//Escribir un algoritmo que multiplique dos números por medio de sumas sucesivas.
Funcion multiplicarSumasSucesivas()
	Definir num1, num2, result, i como Entero
    Escribir "Ingrese el primer número:"
    Leer num1
    Escribir "Ingrese el segundo número:"
    Leer num2
    result= 0
    Para i = 1 Hasta num2 Hacer
        result = result + num1
    FinPara
    Escribir "El resultado de la multiplicación es:", result
FinFuncion
//Ejercicio 14.
//Dados N número positivos (N>1) calcular el promedio de esta serie. Considere que la serie
//termina al leer un 0.
Funcion promedioSerie()
	Definir num, suma, cont como Entero
    Definir prom como Real
    suma <- 0
    cont <- 0
    Escribir "Ingrese los números de la serie (ingrese 0 para terminar):"
    Leer num
    Mientras num <> 0 Hacer
        suma <- suma + num
        cont <- cont + 1
        Leer num
    FinMientras
    Si cont > 0 Entonces
        prom <- suma / cont
        Escribir "El promedio de la serie es:", prom
    Sino
        Escribir "No se ingresaron números en la serie."
    FinSi
FinFuncion
//Ejercicio 15.
//Escribir un algoritmo que divida dos números por medio de restas sucesivas
Funcion divisionRestasSucesivas()
	Definir dividiendo, divisor, cociente como Entero
    Definir resto como Real
    Escribir "Ingrese el dividendo:"
    Leer dividendo
    Escribir "Ingrese el divisor:"
    Leer divisor
    cociente <- 0
    resto <- dividendo
    Mientras resto >= divisor Hacer
        resto <- resto - divisor
        cociente <- cociente + 1
    FinMientras
    Escribir "El cociente de la división es:", cociente
    Escribir "El resto de la división es:", resto
FinFuncion

Algoritmo Proyecto
	Definir menuPrincipal,menuNumeros,menuCadenas,menuArreglos,titulo,titulo2,opc,opcn,apcc,apca Como Caracter
	Definir pos,lim Como Entero
	Dimension menuPrincipal[4],menuSelectivas[11],menuCondicional[11],menuiterativas[16]
	// Arreglo menu principal
	menuPrincipal[0] = "  1)Ejercicios con Estructura Selectivas"
	menuPrincipal[1] = "  2)Ejercicios con Estructura Condicional"
	menuPrincipal[2] = "  3)Ejercicios con Estructura Iterativas"
	menuPrincipal[3] = "  4)Salir"
	
	// arreglo submenu ejercicios con estructura selectivas
	menuSelectivas[0] = "  1)Convierta a expresión matemática"
	menuSelectivas[1] = "  2)Dados dos números calcule la suma, resta, multiplicación, división y módulo."
	menuSelectivas[2] = "  3)Dados tres (3) números, Hacer una aplicación que calcule la resolvente."
	menuSelectivas[3] = "  4)Dados dos lados de un triángulo en cm, calcular la hipotenusa del mismo"
	menuSelectivas[4] = "  5)Dado un número, imprimir 0 si es par y 1 si es impar"
	menuSelectivas[5] = "  6)Dado un (1) número binario de cuatro (4) dígitos imprimir su bit da paridad. El bitde paridad es 1 si el número de bits 1 es impar y 0 en caso contrario."
	menuSelectivas[6] = "  7)Dado un (1) número de cuatro (4) dígitos imprimirlo por separado en unidades,decenas,centenas y unidades de mil."
	menuSelectivas[7] = "  8)Dado un carácter indicar si es un digito o una consonante o un carácter especial."
	menuSelectivas[8] = "  9)Dado dos caracteres indicar si el primer carácter es igual, mayor o menor que el segundo"
	menuSelectivas[9] = "  10)Dada una frase cualquiera presentarla en mayúscula y minuscula"
	menuSelectivas[10]= "  11)Salir"
	
	// arreglos submenu ejercicios con estructura condicional
	menuCondicional[0] = "  1)Año Bisiesto"
	menuCondicional[1] = " 2)5 cifras EsCapicua"
	menuCondicional[2] = "  3)Cree un algoritmo que tome por entrada las horas y minutos de un día y dé comoresultado su equivalente en segundos."
	menuCondicional[3] = "  4)Dados tres números enteros positivos A, B y C, ¿Determine si son iguales? ¿cuál de ellos es el mayor? y ¿cuál es el segundo mayor?"
	menuCondicional[4] = "  5)estacionamiento el monto a pagar"
	menuCondicional[5] = "  6)Dado el peso de una persona en libras (1 libra = 0,453592 Kg) y su estatura encentímetros, calcule su IMC e indique como salida el peso en kilogramos, el valorde IMC de la persona y la categoría en la cual fue clasificado."
	menuCondicional[6] = "  7)Escriba un algoritmo que reciba una fecha (día y mes) correspondiente al año2014"
	menuCondicional[7] = "  8)Solicitar un número entre el 1 y el 12 e imprimir el mes correspondiente a dicho número"
	menuCondicional[8] = "  9)En un almacén se hace un 20% de descuento a los clientes cuya compra supere los $1000,"
	menuCondicional[9] = "  10)Dado dos números y un operador matemático(+,-,*,/,mod,div)"
	menuCondicional[10] = " 11)Salir"
	
	// arreglo submenu aejercicios con estructura iterativas
	menuiterativas[0]= "1)Dado un número entero N, calcular e informar al usuario cuántos dígitos tiene dicho número"
	menuiterativas[1]= "2)Dado un número, determine si es capicúa."
	menuiterativas[2]= "3)Escribir un algoritmo que presente los divisores de un numero"
	menuiterativas[3]= "4)Escribir un algoritmo que presente la suma de los divisores de un numero"
	menuiterativas[4]= "5)Escribir un algoritmo que presente la cantidad de los divisores de un numero"
	menuiterativas[5]= "6)Escribir un algoritmo que indique si un número es perfecto"
	menuiterativas[6]= "7)Dado un número N determinar si es un número primo."
	menuiterativas[7]= "8)Construya un programa que dado un valor entero N, haga el cálculo de la función factorial"
	menuiterativas[8]= "9Dado un número entero N que representa una contraseña "
	menuiterativas[9]= "10)Dada una secuencia de números terminada en cero (0), elaborar un algoritmo que informe al usuario qué valor tiene el número mayor y qué valor tiene el número menor"
	menuiterativas[10]="11)Se tiene una secuencia de enteros terminada en cero, que corresponde a la edad, peso y estatura"
	menuiterativas[11]="12)Construye un algoritmo que calcule e imprima la tabla de multiplicar, desde la tabla del 1 hasta la del 10."
	menuiterativas[12]="13)Escribir un algoritmo que multiplique dos números por medio de sumas sucesivas"
	menuiterativas[13]="14)Dados N número positivos (N>1) calcular el promedio de esta serie"
	menuiterativas[14]="15)Escribir un algoritmo que divida dos números por medio de restas sucesivas"
	menuiterativas[15]="16) Salir"
	opc=""
    Mientras opc <> "4" Hacer
		Borrar Pantalla
		opc=presentarMenu("Menu Principal",menuPrincipal,4)
		Segun opc Hacer
		"1":
			opcn=""
			Mientras opcn <> "11" Hacer         
				opcn=presentarMenu("Estructuras Selectivas",menuSelectivas,11)
				Segun opcn Hacer
					"1":
						Escribir "expresion matematica"
						expresionMatematica()
						Esperar 5 Segundos
					"2":
						Escribir "operaciones basicas entre dos numeros"
						operaciones()
						Esperar 5 Segundos
					"3":
						Escribir "la resolvente de la formula general"
						resolvente()
						Esperar 5 segundos 
					"4"	:
						Escribir "dado los dos lados hallar la hipotenusa de un triangulo "
						hipotenusa()
						Esperar 5 segundos	
					"5":
						Escribir "presentar 0 si es par y 1 si es impar"
						par0Impar1()
						Esperar 5 segundos
					"6":
						Escribir "Imprimir el bit de paridad"
						CalcularParidad()
						Esperar 5 Segundos
					"7":
						Escribir "presentar la unidad, decenas, centenas, mil de una cifra ingresada de cuatro numeros" 
						digitosSeparados()
						Esperar 5 segundos
					"8":
						Escribir "identificar caracter"
						IdentificarCaracter()
						Esperar 5 Segundos
					"9":
						Escribir "caracter mayor igual menor entre dos frases ingresadas"
						caracterIgualMayormenor()
						Esperar 5 segundos
					"10":
						Escribir "presentar frase en mayusculas y minusculas "
					    fraseMayusculaMinuscula()
						Esperar 5 segundos
					"11":
						Escribir "Regresando al Menu Principal"
						Esperar 3 Segundos
					De Otro Modo:
						Escribir "Opcion invalida..."
						Esperar 3 Segundos
				Fin Segun
			FinMientras
		    
		"2":
			Escribir "Menu Codicional"
			opcn=""
			Mientras opcn <> "11" Hacer         
				opcn=presentarMenu("Estructuras Condicionales ",menuCondicional,11)
				Segun opcn Hacer
					"1":
						Escribir "Año Bisiesto"
						AñoBisiesto()
						Esperar 3 Segundos
					"2":
						Escribir ""
						EsCapicua()
						Esperar 5 Segundos
					"3":
						Escribir ""
						equivalenteSegundos()
						Esperar 10 segundos 
					"3.2":
						Escribir ""
						segundosAdiasHorasMinutos()
						Esperar 10 segundos	
					"4":
						Escribir ""
						tresIgualMayorSegundoMayor()
						Esperar 10 segundos
					"5":
						Escribir ""
						montoEstacionamiento()
						Esperar 10 segundos
					"6":
						
						Esperar 5 segundos
					"7":
						Escribir ""
						año2014()
						Esperar 5 segundos
					"8":
						Escribir ""
						numeroDemeses()
						Esperar 5 segundos
					"9":
						Escribir ""
						descuento20compra1000()
						Esperar 5 segundos
					"10":
					    dosNumerosYoperador()
						Esperar 5 segundos
					"11":
						Escribir "Regresando al Menu Principal"
						Esperar 3 Segundos
					De Otro Modo:
						Escribir "Opcion invalida..."
						Esperar 3 Segundos
				Fin Segun
			FinMientras
		"3":
			Escribir "Menu iterativos"
			opcn=""
			Mientras opcn <> "16" Hacer         
				opcn=presentarMenu("Estructuras ",menuIterativas,11)
				Segun opcn Hacer
					"1":
						Escribir "digitos de N numero"
						digitosN()
						Esperar 5 Segundos
					"2":
						Escribir "numero capicua"
						NumeroCapicua()
						Esperar 5 Segundos
					"3":
						Escribir "divisores"
						divisores()
						Esperar 5 segundos 
					"4":
						Escribir "suma de divisores"
						sumaDivisores()
						Esperar 5 segundos
					"5":
						Escribir "cantidad de divisores"
						cantidadDivisores()
						Esperar 5 segundos
					"6":
						Escribir "numero perfecto"
						numPerfecto()
						Esperar 5 segundos
					"7":
						Escribir "Numero primo"
						Nprimo()
						Esperar 5 segundos
					"8":
						Escribir "N factorial"
						Nfactorial()
						Esperar 5 segundos
					"9":
						Escribir "contraseña"
						contraseña()
						Esperar 5 segundos
					"10":
						Escribir"valor mayor y valor menor"
					    valorMayorValorMenor()
						Esperar 5 segundos
					"11":
						Escribir"edad 18 y 35"
						edad18y35()
						Esperar 5 segundos
					"12":
						Escribir"tablas del 1 al 10"
						tablas1al10()
						Esperar 5 segundos
					"13":
						Escribir"multiplicar por Sumas Sucesivas"
						multiplicarSumasSucesivas()
						Esperar 5 segundos
					"14":
						Escribir"promedio de una Serie"
						promedioSerie()
						Esperar 5 segundos
					"15":
						Escribir"division por Restas Sucesivas"
						divisionRestasSucesivas()
						Esperar 5 segundos
					"16":	
						Escribir "Regresando al Menu Principal"
						Esperar 3 Segundos
					De Otro Modo:
						Escribir "Opcion invalida..."
						Esperar 3 Segundos
				Fin Segun
			FinMientras
			Esperar 3 Segundos
		"4":
			Escribir "Gracias por usar el Sistema"
			Esperar 3 Segundos
		Fin Segun
    Fin Mientras
FinAlgoritmo
